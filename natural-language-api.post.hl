
/*
 * HTTP GET endpoint taking natural language input, generating Hyperlambda code solving the problem,
 * and returning the result of the execution to the caller. Response time 1 to 5 seconds typically.
 */
.arguments
   instruction:string

/*
 * System instruction.
 */
.sys:@"# Objective

You are a Hyperlambda software development assistant. Your task is to generate and respond with Hyperlambda.

Rules;
- DO NOT generate Hyperlambda that requires authentication or authorisation unless user EXPLICITLY asks for it!
- DO NOT generate Executable Hyperlambda files or HTTP endpoints unless user EXPLICITLY asks for it!
- Make sure you always return the result of your invocation to the caller."

/*
 * Helper lambda object to store request after execution
 *
 * This lambda object is executed after execution of code,
 * to cache and store the code and prompt, such that we can retrieve
 * the cached version later.
 */
.onafter

   /*
    * Creating a unique filename.
    *
    * Notice, to make it easier to see the code, and edit it,
    * we've chosen to store the actual Hyperlambda code in a physical file on disc.
    */
   strings.to-lower:x:@.arguments/*/prompt
   crypto.hash.sha256:x:-
   .path
   set-value:x:@.path
      strings.concat
         .:"/modules/natural-language-api/cache/hl-"
         get-value:x:@crypto.hash.sha256
         .:.hl

   // Making sure we use prompt as comment.
   hyper2lambda:x:@.arguments/*/code_file
   unwrap:x:+/*/*
   insert-before:x:@hyper2lambda/0
      .
         ..:x:@.arguments/*/prompt
   lambda2hyper:x:@hyper2lambda/*
      comments:true

   // Saving result to cache file.
   io.file.save:x:@.path
      get-value:x:@lambda2hyper

   // Creating database entry for cached item.
   data.connect:natural-language-api
      data.create
         table:requests
         values
            prompt:x:@.arguments/*/prompt
            code_file:x:@.path
            is_error:x:@.arguments/*/is_error

// Contains the code after generation/cache-lookup.
.code_file

// True if code was generated.
.generated:bool:true

// Making sure we trap exceptions
try

   /*
    * Checking cached file to see if this is a previously generated invocation.
    */
   data.connect:natural-language-api
      data.read
         table:requests
         where
            and
               prompt.eq:x:@.arguments/*/instruction

   // Checking if above invocation returned a result.
   if
      exists:x:@data.connect/*/data.read/*
      .lambda

         /*
          * This exact prompt has been previously generated,
          * making sure we use cached version.
          */
         set-value:x:@.code_file
            io.file.load:x:@data.connect/*/data.read/*/*/code_file
         set-value:x:@.generated
            .:bool:false

   else

      /*
       * Invoking Hyperlambda Generator.
       */
      http.post:"https://ainiro.io/magic/modules/hyperlambda-generator/chat"
         convert:true
         payload
            prompt:x:@.arguments/*/instruction
            system_message_override:x:@.sys

      // Sanity checking invocation
      if
         and
            not
               mte:x:@http.post
                  .:int:200
            not
               lt:x:@http.post
                  .:int:300
         .lambda

            // Something went wrong
            lambda2hyper:x:@http.post
            throw:x:-
               public:bool:true
               status:int:400

      // Transforming result to a lambda object.
      set-value:x:@.code_file
         get-value:x:@http.post/*/content/*/result

   // Converting code to lambda object
   hyper2lambda:x:@.code_file

   // Checking if user is root, at which point we just execute "whatever".
   if
      auth.ticket.in-role:root
      .lambda

         // Adding lambda object to [.lambda] invocation.
         add:x:./*/.lambda
            get-nodes:x:@hyper2lambda/*

         // Executing lambda object, and returning the result.
         .lambda
         invoke:x:-

         // Storing request in cache.
         if
            eq:x:@.generated
               .:bool:true
            .lambda
               unwrap:x:+/*
               invoke:x:@.onafter
                  prompt:x:@.arguments/*/instruction
                  code_file:x:@.code_file
                  is_error:int:0

         // Returning result to caller.
         if
            not-null:x:@invoke
            .lambda
               return:x:@invoke
         return-nodes:x:@invoke/*

   else

      /*
       * User is NOT root, so we execute the lambda object within a [whitelist],
       * to avoid anonymous users from creating potential malicious code.
       *
       * None of the whitelisted slots changes state on the server, so worst case scenario,
       * somebody might create a long lasting job, binding up a single thread and socket
       * before returning, bu that is literally the largest risk we've got with this solution,
       * even though we're technically executing arbitrary Hyperlambda code, generated by an AI.
       */

      // Adding lambda object to [whitelist] invocation.
      add:x:./*/whitelist/*/.lambda
         get-nodes:x:@hyper2lambda/*

      // Executing lambda object, and returning the result.
      whitelist
         vocabulary
            add
            and
            apply
            case
            compose
            convert
            csv2lambda
            default
            else
            else-if
            eq
            exists
            floatArray2bytes
            for-each
            fork
            format
            get-count
            get-first-value
            get-name
            get-nodes
            get-value
            html-decode
            html2lambda
            html2markdown
            html2pdf
            hyper2lambda
            if
            include
            insert-after
            insert-before
            int2words
            join
            json2lambda
            json2yaml
            lambda2csv
            lambda2html
            lambda2hyper
            lambda2json
            lambda2xml
            lambda2yaml
            lt
            lte
            markdown2html
            mt
            mte
            neq
            not
            not-exists
            not-null
            null
            or
            pdf2text
            reference
            remove-nodes
            return
            return-nodes
            return-value
            semaphore
            set-name
            set-value
            set-x
            sleep
            sort
            switch
            throw
            time
            try
            type
            types
            unwrap
            version
            vocabulary
            while
            xml2lambda
            yaml2json
            yaml2lambda
            yield
            auth.ticket.get
            auth.ticket.in-role
            auth.ticket.verify
            auth.token.verify
            cache.count
            crypto.aes.decrypt
            crypto.aes.encrypt
            crypto.decrypt
            crypto.encrypt
            crypto.fingerprint
            crypto.get-key
            crypto.hash
            crypto.hash.md5
            crypto.hash.sha1
            crypto.hash.sha256
            crypto.hash.sha384
            crypto.hash.sha512
            crypto.password.hash
            crypto.password.verify
            crypto.random
            crypto.random.int
            crypto.rsa.create-key
            crypto.rsa.decrypt
            crypto.rsa.encrypt
            crypto.rsa.sign
            crypto.rsa.verify
            crypto.seed
            crypto.sign
            crypto.verify
            date.format
            date.from-unix
            date.max
            date.min
            date.now
            date.unix
            endpoints.list
            guid.new
            http.delete
            http.get
            image.generate-qr
            math.abs
            math.add
            math.ceil
            math.cos
            math.decrement
            math.divide
            math.dot
            math.floor
            math.increment
            math.max
            math.min
            math.modulo
            math.multiply
            math.random
            math.round
            math.sin
            math.sqrt
            math.subtract
            mime.create
            mime.parse
            openai.tokenize
            request.cookies.get
            request.cookies.list
            request.headers.get
            request.headers.list
            request.host
            request.ip
            request.scheme
            request.url
            request.verb
            response.cookies.set
            response.headers.set
            response.status.set
            server.ip
            slots.vocabulary
            strings.builder
            strings.builder.append
            strings.byte-count
            strings.capitalize
            strings.concat
            strings.contains
            strings.ends-with
            strings.html-decode
            strings.html-encode
            strings.join
            strings.length
            strings.matches
            strings.mixin
            strings.regex-replace
            strings.replace
            strings.replace-not-of
            strings.split
            strings.starts-with
            strings.substring
            strings.to-lower
            strings.to-upper
            strings.trim
            strings.trim-end
            strings.trim-start
            strings.url-decode
            strings.url-encode
            time.format
            time.total-milliseconds
            validators.date
            validators.default
            validators.email
            validators.enum
            validators.integer
            validators.mandatory
            validators.recaptcha
            validators.regex
            validators.string
            validators.url
         .lambda

      // Storing request in cache.
      if
         eq:x:@.generated
            .:bool:true
         .lambda
            unwrap:x:+/*
            invoke:x:@.onafter
               prompt:x:@.arguments/*/instruction
               code_file:x:@.code_file
               is_error:int:0

      // Returning result to caller.
      if
         not-null:x:@whitelist
         .lambda
            return:x:@whitelist
      return-nodes:x:@whitelist/*

.catch

   // Oops ...!!
   log.error:Could not generate code
      exception:x:@.arguments/*/message

   // Throwing exception
   throw:"I’m sorry, Dave. I’m afraid I can’t do that."
      public:bool:true
      status:int:400
