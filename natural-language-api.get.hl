.arguments
   instruction:string

/*
 * System instruction.
 */
.instruction:@"# Objective

You are a Hyperlambda software development assistant. Your task is to generate and respond with Hyperlambda.
DO NOT generate Hyperlambda that requires authentication or authorisation.
DO NOT generate Executable Hyperlambda files or HTTP endpoints unless user EXPLICITLY asks for it!
Make sure you always return the result of your invocation to the caller."

/*
 * Invoking OpenAI with Hyperlambda Generator as model, and above instruction as our system instruction.
 */
execute:magic.workflows.actions.execute
   name:openai-query
   filename:/modules/openai/workflows/actions/openai-query.hl
   arguments
      model:"ft:gpt-4.1-mini-2025-04-14:ainiro:hl:CgrJqwb2"
      max_tokens:int:2500
      temperature:decimal:0.0
      query:x:@.arguments/*/instruction
      instruction:x:@.instruction

// Transforming result to a lambda object.
hyper2lambda:x:@execute/*/answer

// Adding lambda object to [whitelist] invocation.
add:x:./*/whitelist/*/.lambda
   get-nodes:x:@hyper2lambda/*
lambda2hyper:x:../*/whitelist
log.info:x:-

// Executing lambda object, and returning the result.
whitelist
   vocabulary
      set-name
      set-value
      get-value
      get-name
      get-count
      get-nodes
      add
      insert-after
      insert-before
      math.abs
      math.add
      math.ceil
      math.cos
      math.decrement
      math.divide
      math.floor
      math.increment
      math.max
      math.min
      math.modulo
      math.multiply
      math.random
      math.round
      math.sin
      math.sqrt
      math.subtract
      strings.builder
      strings.builder.append
      strings.byte-count
      strings.capitalize
      strings.concat
      strings.contains
      strings.ends-with
      strings.html-decode
      strings.html-encode
      strings.join
      strings.length
      strings.matches
      strings.regex-replace
      strings.replace
      strings.replace-not-of
      strings.split
      strings.starts-with
      strings.substring
      strings.to-lower
      strings.to-upper
      strings.trim
      strings.trim-end
      strings.trim-start
      strings.url-decode
      strings.url-encode
      response.headers.set
      response.status.set
      for-each
      if
      else-if
      else
      return
      return-nodes
      yield
      vocabulary
      html2lambda
      html2markdown
      lambda2html
      markdown2html
      xml2lambda
      lambda2xml
      json2lambda
      lambda2json
      http.get
      and
      or
      eq
      neq
      not
      mt
      lt
      mte
      lte
      get-name
      null
      not-null
      exists
      not-exists
      unwrap
      convert
   .lambda
if
   not-null:x:@whitelist
   .lambda
      return:x:@whitelist
return-nodes:x:@whitelist/*